&НаКлиенте
Перем МестныйКэш Экспорт;
// функции для совместимости кода 
&НаКлиенте
Функция сбисПолучитьФорму(ИмяФормы)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Попытка
			ЭтотОбъект="";
		Исключение
		КонецПопытки;
		Возврат ПолучитьФорму("ВнешняяОбработка.СБИС.Форма."+ИмяФормы);
	КонецЕсли;
	Возврат ЭтотОбъект.ПолучитьФорму(ИмяФормы);
КонецФункции
&НаКлиенте
Функция сбисЭлементФормы(Форма,ИмяЭлемента)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Возврат Форма.Элементы.Найти(ИмяЭлемента);
	КонецЕсли;
	Возврат Форма.ЭлементыФормы.Найти(ИмяЭлемента);
КонецФункции
//------------------------------------------------------
&НаКлиенте
Функция ПоказатьДокументы(Кэш,МассивПакетов) Экспорт
// Функция открывает форму и заполняет все данные на ней	
	МестныйКэш = Кэш;
	СписокПакетов.Очистить();
	ОтметитьВсе=Истина;
	Для Каждого Пакет из МассивПакетов Цикл
		НоваяСтрока = СписокПакетов.Добавить();
		НоваяСтрока.НазваниеПакета = пакет.вложение[0].название;
		НоваяСтрока.Пакет.добавить(пакет);
		НоваяСтрока.Отмечен= истина;
		Если Пакет.Свойство("Контрагент") Тогда
			Если Пакет.Контрагент.Свойство("СвЮЛ") Тогда
				НоваяСтрока.ПакетКонтрагент = Пакет.Контрагент.СвЮЛ.Название;
			ИначеЕсли Пакет.Контрагент.Свойство("СвФЛ") Тогда
				НоваяСтрока.ПакетКонтрагент = Пакет.Контрагент.СвФЛ.Фамилия+" "+Пакет.Контрагент.СвФЛ.Имя+?(Пакет.Контрагент.СвФЛ.Свойство("Отчество")," "+Пакет.Контрагент.СвФЛ.Отчество,"");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	сбисЭлементФормы(ЭтаФорма, "НадписьПодготовленныеДокументы").Заголовок= "Подготовлено пакетов: "+СписокПакетов.Количество();
	ЭтаФорма.Открыть();
КонецФункции

&НаКлиенте
Процедура ОтправитьНажатие(Команда)
	// Процедура отправляет список выбранных пакетов документ
	МассивПодготовленныхПакетов = Новый Массив;
	Для Каждого СтрокаТаблицы из СписокПакетов Цикл
		Если СтрокаТаблицы.Отмечен Тогда
			МассивПодготовленныхПакетов.Добавить(СтрокаТаблицы.пакет[0].значение);
		КонецЕсли;
	КонецЦикла;
	ВремяНачала = ТекущаяУниверсальнаяДатаВМиллисекундах();			
	МестныйКэш.Вставить("РезультатОтправки", Новый Структура("ТипыОшибок,Отправлено,НеОтправлено,НеСформировано,Ошибок,ДетализацияОшибок,ВсегоПакетов,ОшибкиДоОтправки,ДанныеПоСтатусам,ПорНомер,КоличествоСвободныхПотоков,ФормаОтправки,ОтправленоСообщений,ПолученоОтветов,ВремяНачала,ВремяФормирования,ВремяОтправки,ДетализацияОтправки,ВремяЗаписиСтатусов,ВремяПолученияДанных,ВремяОжиданияОтвета, СформированныеПакеты", Новый СписокЗначений,0,0,0,0,Новый Соответствие,0,0,Новый Массив,0,30,"ФормаПросмотрДокументов",0,0,ВремяНачала,0,0, Новый Соответствие,0,0,0, Новый Соответствие));
	МестныйКэш.РезультатОтправки.Вставить("НаЗаписьСтатусов", Новый Структура("Ошибки, Ответы", Новый Соответствие, Новый Соответствие));
	
	сбисПолучитьФорму("Документ_Шаблон").ОтправитьПодготовленныеПакетыДокументы(МестныйКэш, МассивПодготовленныхПакетов);
КонецПроцедуры
&НаКлиенте
Процедура ПослеОтправки(Кэш) Экспорт
// Если есть ошибки отправки, то выводим результат, иначе закрываем просмотр 	
	Если Кэш.РезультатОтправки.НеОтправлено>0 Тогда  // если есть ошибки при отправке, показываем окно результатов
		фрм = Кэш.ГлавноеОкно.сбисНайтиФормуФункции("ПоказатьРезультатОтправки","ФормаРезультатОтправки","", Кэш);
		фрм.ПоказатьРезультатОтправки(Кэш);	
	Иначе    // если отправилось без ошибок, закрываем окно просмотра
		ОбновлятьГлавноеОкно = Истина;
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура СписокПакетовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	КоманднаяПанельСписокПакетовПросмотр(неопределено);
КонецПроцедуры

&НаКлиенте
Процедура КоманднаяПанельСписокПакетовПросмотр(Команда)
	Если сбисЭлементФормы(ЭтаФорма, "СписокПакетов").ТекущиеДанные<>Неопределено Тогда
		ПолныйСоставПакета = сбисЭлементФормы(ЭтаФорма, "СписокПакетов").ТекущиеДанные.пакет[0].значение;
		Если ПолныйСоставПакета.Свойство("Вложение") Тогда
			Для Каждого Элемент Из ПолныйСоставПакета.Вложение Цикл
				Если Элемент.Свойство("XMLДокумента") И ЗначениеЗаполнено(Элемент.XMLДокумента) Тогда
					ТекстHTML = МестныйКэш.Интеграция.ПолучитьHTMLПоXML(МестныйКэш, Элемент);
				Иначе
					ТекстHTML = "";
				КонецЕсли;
				Элемент.Вставить("ТекстHTML",ТекстHTML);
			КонецЦикла;
			фрм = МестныйКэш.ГлавноеОкно.сбисНайтиФормуФункции("ПоказатьДокумент","ФормаПросмотрДокумента","", МестныйКэш);
			фрм.ПоказатьДокумент(МестныйКэш,ПолныйСоставПакета);	
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	ОтметитьВсе = Не ОтметитьВсе;
	Для Каждого Пакет из СписокПакетов Цикл
		Пакет.Отмечен= ОтметитьВсе;
	конеццикла;
КонецПроцедуры
&НаКлиенте
Процедура ПриЗакрытии()
	// Обновляет контент на главном окне.
	ФормаГлавногоОкна = МестныйКэш.ГлавноеОкно;
	Если ФормаГлавногоОкна.Открыта() и ОбновлятьГлавноеОкно = Истина Тогда
		ФормаГлавногоОкна.ОбновитьКонтент();
	КонецЕсли;
	
	фрм = ФормаГлавногоОкна.сбисНайтиФормуФункции("сбисПослеЗакрытияФормыПросмотра","РаботаСДокументами1С","", МестныйКэш);
	Если фрм<>Ложь Тогда
		фрм.сбисПослеЗакрытияФормыПросмотра(МестныйКэш); 
	КонецЕсли; 
КонецПроцедуры
