&НаКлиенте
Перем Кэш;

//функция обновляет контент для подразделов раздела Продажа	
&НаКлиенте
Функция ОбновитьКонтент(ЛокальныйКэш) Экспорт
	//СтруктураДляОбновленияФормы = Кэш.ОбщиеФункции.сбисОбновитьРеестрДокументов1С(Ини, Кэш);
	Кэш = ЛокальныйКэш;
	ИмяРеестра = "";
	ИмяРаздела = "Справочники";	
	Ини = "";
	Если	Не Кэш.Разделы[ИмяРаздела].Свойство(Кэш.Текущий.Имя, ИмяРеестра)
		Или	Не Кэш.Ини.Свойство(ИмяРеестра,Ини) Тогда
		Возврат Ложь;
	ИначеЕсли Ини = Неопределено Тогда
		Ини = Кэш.ФормаНастроек.Ини(Кэш, ИмяРеестра);
	КонецЕсли;
	Кэш.Текущий.ТипДок = ИмяРеестра;
	ГлавноеОкно				= Кэш.ГлавноеОкно;
	Контент					= ГлавноеОкно.сбисЭлементФормы(ГлавноеОкно, "Контент");
	Кэш.ТаблДок				= ГлавноеОкно.сбисЭлементФормы(ГлавноеОкно, "Таблица_РеестрСправочников");
	Контент.ТекущаяСтраница	= ГлавноеОкно.сбисПолучитьСтраницу(Контент, "РеестрСправочников");	
	
	СтраницыПанели = Кэш.ОбщиеФункции.СбисПолучитьЭлементФормы(Кэш,, ИмяРаздела + "_Подразделы");
	СтраницаОперацийРеестра = Кэш.ОбщиеФункции.СбисПолучитьЭлементФормы(Кэш, СтраницыПанели, ИмяРеестра);
	Если Не СтраницаОперацийРеестра = Неопределено Тогда
		СтраницыПанели.ТекущаяСтраница = СтраницаОперацийРеестра;
	КонецЕсли;
	
	НаСменуРаздела(Кэш);//Вызываем руками, чтобы изменилась панель операций.
	Если Не ЗначениеЗаполнено(ГлавноеОкно.ТипЦен) Тогда
		мНоменклатура = Кэш.ФормаНастроек.Ини(Кэш, ИмяРеестра);
		Если	мНоменклатура.Свойство("мФайл",			мНоменклатура) 
			И	мНоменклатура.Свойство("Номенклатура",	мНоменклатура) Тогда
			Если мНоменклатура.Свойство("ТипЦенНоменклатуры") Тогда
				ИмяСправочника = СокрЛП(Сред(мНоменклатура.ТипЦенНоменклатуры.Значение, Найти(мНоменклатура.ТипЦенНоменклатуры.Значение, ".")+1));
				ТипСправочника = "СправочникСсылка." + ИмяСправочника;
			Иначе
				ТипСправочника = "СправочникСсылка.ТипыЦенНоменклатуры";
			КонецЕсли;
			ОписаниеТипа = Новый ОписаниеТипов(ТипСправочника);
			ГлавноеОкно.сбисЭлементФормы(ГлавноеОкно,"ТипЦен").ОграничениеТипа = ОписаниеТипа;
			ГлавноеОкно.ТипЦен = ОписаниеТипа.ПривестиЗначение();
		КонецЕсли;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ГлавноеОкно.ДатаЦен) Тогда
		ГлавноеОкно.ДатаЦен = ТекущаяДата();	
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ГлавноеОкно.ВладелецКаталога) Тогда
		Если Кэш.Ини.Конфигурация.Свойство("Организации") Тогда
			ИмяСправочника = СокрЛП(Сред(Кэш.Ини.Конфигурация.Организации.Значение, Найти(Кэш.Ини.Конфигурация.Организации.Значение, ".")+1));
			ТипСправочника = "СправочникСсылка."+ИмяСправочника;
		Иначе
			ТипСправочника = "СправочникСсылка.Организации";
		КонецЕсли;
		ОписаниеТипа = Новый ОписаниеТипов(ТипСправочника);
		ГлавноеОкно.сбисЭлементФормы(ГлавноеОкно,"ВладелецКаталога").ОграничениеТипа = ОписаниеТипа;
		ГлавноеОкно.ВладелецКаталога = ОписаниеТипа.ПривестиЗначение();
	КонецЕсли;

	ГлавноеОкно.сбисЗаполнитьСписокСправочника(Кэш.ФормаНастроек.Ини(Кэш, ИмяРеестра));
КонецФункции
&НаКлиенте
Процедура НастроитьКолонки(Кэш) Экспорт
КонецПроцедуры
//Процедура устанавливает панель навигации на 1ую страницу и скрывает панель	
&НаКлиенте
Процедура НавигацияУстановитьПанель(ЛокальныйКэш=Неопределено) Экспорт
	ГлавноеОкно = Кэш.ГлавноеОкно;
	ГлавноеОкно.сбисЭлементФормы(ГлавноеОкно, "ПанельНавигации").Видимость		= Ложь;
	ГлавноеОкно.сбисЭлементФормы(ГлавноеОкно, "ЗаписейНаСтранице1С").Видимость	= Ложь;
	ГлавноеОкно.сбисЭлементФормы(ГлавноеОкно, "ЗаписейНаСтранице").Видимость	= Ложь;
КонецПроцедуры

&НаКлиенте
Процедура НаСменуРаздела(ЛокальныйКэш) Экспорт
	Кэш = ЛокальныйКэш;
	ПанельМассовыхОпераций = Кэш.ГлавноеОкно.сбисЭлементФормы(Кэш.ГлавноеОкно, "ПанельМассовыхОпераций");
	Попытка
		ПанельМассовыхОпераций.ТекущаяСтраница = Кэш.ГлавноеОкно.сбисПолучитьСтраницу(ПанельМассовыхОпераций,"Справочники" + Кэш.Текущий.ТипДок);
		Кэш.ГлавноеОкно.сбисЭлементФормы(Кэш.ГлавноеОкно,"ПанельТулбар").Видимость = Истина;
	Исключение
		ПанельМассовыхОпераций.ТекущаяСтраница = Кэш.ГлавноеОкно.сбисПолучитьСтраницу(ПанельМассовыхОпераций,"Справочники");
		Кэш.ГлавноеОкно.сбисЭлементФормы(Кэш.ГлавноеОкно,"ПанельТулбар").Видимость = Истина;
	КонецПопытки;	
	ТекущийИмя	= Кэш.Текущий.Имя;
	РодительИмя	= Кэш.Текущий.ПредыдущийРаздел.ТипДок;
	КнопкиПрайс = Кэш.ГлавноеОкно.сбисЭлементФормы(Кэш.ГлавноеОкно, "СправочникиОтправить"); 
	Если Кэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
		КнопкиПрайс = КнопкиПрайс.ПодчиненныеЭлементы;
	Иначе
		КнопкиПрайс = КнопкиПрайс.Кнопки;
	КонецЕсли;
	Кэш.ГлавноеОкно.сбисОчиститьПанельКнопок(Новый Структура("Имя, УправляемоеПриложение", "СправочникиОтправить", Кэш.ПараметрыСистемы.Клиент.УправляемоеПриложение), Ложь);
	
	сбисРазделМеню = Кэш.МенюРазделов;
	//Заполним команды из инишек и стандартные
	Если	сбисРазделМеню.Свойство(РодительИмя,	сбисРазделМеню)
		И	сбисРазделМеню.Свойство(ТекущийИмя,		сбисРазделМеню) Тогда
		ДобавитьКнопки = Новый Массив;
		ДобавитьКнопки.Добавить(Новый Структура("ИмяИни, ИмяКнопки, МетодАктивации, ЗаголовокКнопки", "Номенклатура", "ОтправитьПрайс",		"ОтправитьПрайс",	"Отправить прайс"));
		ДобавитьКнопки.Добавить(Новый Структура("ИмяИни, ИмяКнопки, МетодАктивации, ЗаголовокКнопки", "Номенклатура", "ОтправитьКаталог",	"ОтправитьКаталог", "Загрузить каталог на sbis.ru"));
		//Проверим, не переназначены ли стандартные команды через ини
		Для Каждого Пункт Из сбисРазделМеню Цикл
			Для сч = 0 По ДобавитьКнопки.Количество()-1 Цикл
				Если ДобавитьКнопки[сч]["ИмяКнопки"] = Пункт["ИмяКнопки"] Тогда
					ДобавитьКнопки.Удалить(сч);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Для Каждого КнопкаДобавить Из ДобавитьКнопки Цикл
			сбисРазделМеню.Добавить(КнопкаДобавить);
		КонецЦикла;
	Иначе
		Возврат;
	КонецЕсли;
	//Добавим кнопки в Отправить
	Для Каждого Пункт Из сбисРазделМеню Цикл
		Если Не ЗначениеЗаполнено(Пункт.МетодАктивации) Тогда
			Продолжить;
		КонецЕсли;
		Отказ			= Ложь;
		ПараметрыКнопки = Новый Структура(
			"Имя,				Заголовок,																		Панель,					УправляемоеПриложение", 
			Пункт.ИмяКнопки,	?(Пункт.Свойство("ЗаголовокКнопки"), Пункт.ЗаголовокКнопки, Пункт.ИмяКнопки),	"СправочникиОтправить",	Кэш.ПараметрыСистемы.Клиент.УправляемоеПриложение);
		РезультатФормированияКнопки = Кэш.ГлавноеОкно.сбисДобавитьКнопку(ПараметрыКнопки, Отказ);
		Если Отказ Тогда
			Кэш.ГлавноеОкно.сбисСообщитьОбОшибке(Кэш, РезультатФормированияКнопки);
		КонецЕсли;
	КонецЦикла;		
КонецПроцедуры