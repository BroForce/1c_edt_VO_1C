&НаКлиенте
Перем МестныйКэш Экспорт;

////////////////////////////////////////////////////
//////////////////Действия формы////////////////////
////////////////////////////////////////////////////

//Функция открывает форму сопоставления одного документа и заполняет все данные на ней	
&НаКлиенте
Функция Показать(Кэш, оДокумент, СохранитьРезультат=Истина) Экспорт
	Перем сбисРезультат;
	
	ПараметрыФормыИнициировать = Новый Структура("оДокумент, СохранитьРезультат", оДокумент, СохранитьРезультат);
	ИнициироватьФормуСопоставления(Кэш, ПараметрыФормыИнициировать);
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		сбисРезультат = ОткрытьМодально();
	#Иначе
		//Для всех прочих, не модальных клиентов идём оценивать результат в описаниеоповещния.
		Открыть();
	#КонецЕсли	
	Возврат сбисРезультат;
КонецФункции

//Закрывает форму, возвращает выбранный результат
&НаКлиенте
Процедура ЗакрытьФорму(СохранитьЗнч)
	сбисРезультат = Ложь;
	Если сбисПараметры.СохранитьРезультат Тогда
		Если сбисПараметры.ОбновитьГлавноеОкно Тогда
			сбисРезультат = Истина;
		КонецЕсли;
	ИначеЕсли СохранитьЗнч Тогда
		сбисРезультат = Документ1С;
	КонецЕсли;
	Закрыть(сбисРезультат);	
КонецПроцедуры

////////////////////////////////////////////////////
///////////////////Кнопки формы/////////////////////
////////////////////////////////////////////////////

//Процедура открывает карточку документа 1С из списка подходящих документов	
&НаКлиенте
Процедура КоманднаяПанельПодходящиеДокументыПросмотр(Элемент)
	
	сбисЭлементДанные = МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "ПодходящиеДокументы").ТекущиеДанные;
	Если сбисЭлементДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		сбисЭлементДанные.Документ1С.ПолучитьФорму().ОткрытьМодально();
	#Иначе
		ПоказатьЗначение(Неопределено, сбисЭлементДанные.Документ1С);
	#КонецЕсли
	
КонецПроцедуры

//Процедура сопоставляет выбранный документ 1С документу сбис	
&НаКлиенте
Процедура КоманднаяПанельПодходящиеДокументыВыбрать(Элемент)
	
	сбисЭлементДанные = МестныйКэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма, "ПодходящиеДокументы").ТекущиеДанные;
	Если сбисЭлементДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Документ1С = сбисЭлементДанные.Документ1С;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНажатие(Элемент)
	
	ЗакрытьФорму(Истина);
	
КонецПроцедуры

//Процедура сопоставляет выбранный документ 1С документу сбис	
&НаКлиенте
Процедура ПодходящиеДокументыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Документ1С = Элемент.ТекущиеДанные.Документ1С;
	ЗакрытьФорму(Истина);
	
КонецПроцедуры

//Процедура устанавливает ограничения типов документов 1С, которые можно выбрать для сопоставления	
&НаКлиенте
Процедура УстановитьОграничениеТипа(Элемент, Типы1С)
	
	МассивТипов = Новый Массив;
	Для Каждого Тип1С Из Типы1С Цикл
		МассивТипов.Добавить(Тип("ДокументСсылка."+Тип1С.Значение));
	КонецЦикла;
	Элемент.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	
КонецПроцедуры

////////////////////////////////////////////////////
///////////////////События формы////////////////////
////////////////////////////////////////////////////

//Инциирует форму сопоставления, заполняет параметры работы
&НаКлиенте
Процедура ИнициироватьФормуСопоставления(Кэш, сбисПараметрыФормы) Экспорт
	МестныйКэш = Кэш;
	Если сбисПараметры = Неопределено Тогда
		сбисПараметры = Новый Структура("оДокумент");
	КонецЕсли;
	Если ЗначениеЗаполнено(сбисПараметрыФормы) Тогда
		Для Каждого КлючИЗначениеПараметры Из сбисПараметрыФормы Цикл
			сбисПараметры.Вставить(КлючИЗначениеПараметры.Ключ, КлючИЗначениеПараметры.Значение);
		КонецЦикла;
	КонецЕсли;
	Если Не сбисПараметры.Свойство("СохранитьРезультат") Тогда
		сбисПараметры.Вставить("СохранитьРезультат", Истина);
	КонецЕсли;
	Если Не сбисПараметры.Свойство("ОбновитьГлавноеОкно") Тогда
		сбисПараметры.Вставить("ОбновитьГлавноеОкно", Ложь);
	КонецЕсли;
	сбисПараметры.Вставить("Инициирована", Истина);
	
	оДокумент = сбисПараметры.оДокумент;
	УстановитьОграничениеТипа(Кэш.ГлавноеОкно.сбисЭлементФормы(ЭтаФорма,"Документ1С"), оДокумент.Типы1С);	
	Если оДокумент.Документ1С<> Неопределено Тогда
		Документ1С   = оДокумент.Документ1С;
	ИначеЕсли МестныйКэш.СБИС.ини.Свойство(оДокумент.ТипСБИС) и МестныйКэш.СБИС.ини[оДокумент.ТипСБИС].Количество()=1 Тогда
		Документ1С = МестныйКэш.СБИС.ини[оДокумент.ТипСБИС][0].ПустаяСсылка;
	КонецЕсли;

 	ДокументНазвание = оДокумент.ДокументСБИСНазвание;
	ПодходящиеДокументы.Очистить();
	Для Каждого Док из оДокумент.СписокПодходящихДокументов Цикл
		НоваяСтрока = ПодходящиеДокументы.Добавить();
		НоваяСтрока.Документ1С = Док.Значение;
	КонецЦикла;	
	
	Если оДокумент.СписокПодходящихДокументов.Количество()=1 И (Документ1С=Неопределено или Документ1С.Пустая()) Тогда
		Документ1С = оДокумент.СписокПодходящихДокументов[0].Значение;
	КонецЕсли;
КонецПроцедуры

//Оформление таблицы подходящих документов ОФ	
&НаКлиенте
Процедура ПодходящиеДокументыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.Документ1С.Текст = МестныйКэш.ОбщиеФункции.СформироватьНазваниеВходящегоДокумента1С(ДанныеСтроки.Документ1С);
	
КонецПроцедуры

//На случай использования через ОткрытьФорму()
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	сбисПараметры = Новый Структура("Инициирована", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии()
	Если Не сбисПараметры.Инициирована Тогда
		//Произошло открытие через ОткрытьФорму()
		//Возьмём кэш у владельца и запустим инициализацию с Параметры
		Если МестныйКэш = Неопределено Тогда
			МестныйКэш = ЭтаФорма.ВладелецФормы.МестныйКэш;
		КонецЕсли;
		ИнициироватьФормуСопоставления(МестныйКэш, Параметры);
	КонецЕсли;
КонецПроцедуры

