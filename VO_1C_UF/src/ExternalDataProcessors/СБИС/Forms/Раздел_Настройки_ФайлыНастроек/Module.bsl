перем Кэш Экспорт;
// функции для совместимости кода 
&НаКлиенте
Функция сбисПолучитьФорму(ИмяФормы)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Попытка
			ЭтотОбъект="";
		Исключение
		КонецПопытки;
		Возврат ПолучитьФорму("ВнешняяОбработка.СБИС.Форма."+ИмяФормы);
	КонецЕсли;
	Возврат ЭтотОбъект.ПолучитьФорму(ИмяФормы);
КонецФункции
&НаКлиенте
Функция сбисЭлементФормы(Форма,ИмяЭлемента)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Возврат Форма.Элементы.Найти(ИмяЭлемента);
	КонецЕсли;
	Возврат Форма.ЭлементыФормы.Найти(ИмяЭлемента);
КонецФункции
&НаКлиенте
Функция сбисПолучитьСтраницу(Элемент, ИмяСтраницы)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Возврат Элемент.ПодчиненныеЭлементы[ИмяСтраницы];
	КонецЕсли;
	Возврат Элемент.Страницы[ИмяСтраницы];
КонецФункции
//------------------------------------------------------
&НаКлиенте
Функция ОбновитьКонтент(Кэш) Экспорт
	// Функция обновляет данные в главном окне при переходе в раздел Файлы настроек
	ГлавноеОкно = Кэш.ГлавноеОкно;
	Контент = сбисЭлементФормы(ГлавноеОкно, "Контент");
	Контент.ТекущаяСтраница = сбисПолучитьСтраницу(Контент, "ФайлыНастроек");
	Отказ = Ложь;
	Кэш.ФормаНастроек.ПриПереходеВРазделФайловНастроек(Кэш, Новый Структура("КэшироватьВсеПолучаемые", Истина), Отказ);		
	Если Не Кэш.Свойство("Конфигурация") Тогда
		ТекущееЗначениеКонфигурации = ГлавноеОкно.Конфигурация;
		ГлавноеОкно.ТабДоступныеИниФайлы.Очистить();
		сбисЭлементФормы(ГлавноеОкно, "Конфигурация").СписокВыбора.Очистить();
		
		СписокТиповНастроек = Кэш.ФормаНастроек.сбисПолучитьСписокДоступныхНастроек(Кэш);
		
		Если СписокТиповНастроек = Неопределено Тогда
			Возврат	Ложь;
		КонецЕсли;
		
		ДанныеКонфигураций = Кэш.ФормаНастроекОбщее.СформироватьСтруктуруКонфигураций(Кэш, СписокТиповНастроек);
		сбисЭлементФормы(ГлавноеОкно, "Конфигурация").СписокВыбора.ЗагрузитьЗначения(ДанныеКонфигураций.СписокВыбора);
		
		ГлавноеОкно.Кэш.Вставить("Конфигурация", ДанныеКонфигураций.СтруктураКонфигураций);
		ГлавноеОкно.Конфигурация = ТекущееЗначениеКонфигурации;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ГлавноеОкно.Конфигурация) Тогда 		
		Префикс = Кэш.ФормаНастроекОбщее.ПолучитьМассивФайловДляКонфигурации(СписокТиповНастроек, Кэш.КэшНастроек.ПараметрыНастроек.ПрефиксСервис, Новый Структура("ЗначениеПредставление","Представление"));
		ГлавноеОкно.Конфигурация = Кэш.ФормаНастроекОбщее.ОпределитьТипНастроекПоПрефиксу(Кэш.КэшНастроек.ПараметрыНастроек.ПрефиксСервис, Префикс);
	КонецЕсли;
	УстановленныеИниФайлы = Кэш.ФормаНастроек.сбисПолучитьМассивУстановленныхИниФайлов(Кэш);
	
	ГлавноеОкно.сбисОбновитьИнформациюНастроекНаГлавномОкне(Кэш, УстановленныеИниФайлы);
	
	Возврат Истина;
	
КонецФункции
&НаКлиенте
Процедура НаСменуРаздела(Кэш) Экспорт
	
	Если Не ЗначениеЗаполнено(Кэш.ГлавноеОкно.Конфигурация)	Тогда
		ОбновитьКонтент(Кэш);
	КонецЕсли;
	сбисЭлементФормы(Кэш.ГлавноеОкно,"ПанельТулбар").Видимость = Ложь;
	
КонецПроцедуры
&НаКлиенте
Процедура НастроитьКолонки(Кэш) Экспорт
КонецПроцедуры
&НаКлиенте
Процедура НавигацияУстановитьПанель(Кэш) Экспорт
	// Процедура устанавливает панель навигации на 1ую страницу и скрывает панель	
	ГлавноеОкно = Кэш.ГлавноеОкно;
	сбисЭлементФормы(ГлавноеОкно,"ПанельНавигации").Видимость=Ложь;
КонецПроцедуры	
